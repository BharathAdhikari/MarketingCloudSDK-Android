apply plugin: 'com.android.application'

android {
    /* Change these to fit your applications requirements! */
    /* 01 release brings support for android 23 (Marshmallow) */
    compileSdkVersion 23
    /* Pickup latest build tools from SDK installation - Thanks @androidfu */
    Properties localProps = new Properties()
    localProps.load(new FileInputStream(file('../local.properties')))
    def buildToolVer = file("${localProps['sdk.dir']}/build-tools/").listFiles().sort().reverse().first().name;
    buildToolsVersion buildToolVer;
    
    defaultConfig {
        applicationId "com.salesforce.marketingcloud.android.demoapp"
        minSdkVersion 16 // MIN SDK for use with ETPush is 15, but this app uses 16 for maps compatibility
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testApplicationId "com.salesforce.marketingcloud.android.demoapp.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "d"
        }
    }
}

dependencies {
    /* SDK */
    compile 'com.exacttarget.etpushsdk:etsdk:4.2.0@aar'

    /* Google's Support v4 for Notification compatibility */
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:design:23.1.1'

    /* Google Play Services for Location and Google Cloud Messaging */
    compile 'com.google.android.gms:play-services-location:8.1.0'
    compile 'com.google.android.gms:play-services-gcm:8.1.0'

    /* 3rd Party Libraries Required for SDK integration */
    compile 'org.altbeacon:android-beacon-library:2.5.1@aar'
}
